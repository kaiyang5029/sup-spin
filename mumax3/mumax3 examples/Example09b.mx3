// Regions of material parameters
Aex_val := 6e-12
Msat_val := 0.5e6
Ku1_val := 142920
Dbulk_val := 0
Dind_val := 1e-3
alpha_val := 0.1

// Region 0 - Effective medium 1
Aex.SetRegion(0, Aex_val) // Exchange in J/m
Msat.SetRegion(0, Msat_val) //Saturation magnetisation in A/m
Ku1.SetRegion(0, Ku1_val*5) // Anisotropy in J/m^3
AnisU.SetRegion(0, vector(0, 0, 1)) //Uniaxial anisotropy direction 
Dbulk.SetRegion(0, Dbulk_val)
Dind.SetRegion(0, Dind_val) //Interfacial DMI in J/m^2
alpha.SetRegion(0, alpha_val) // zDamping

// Region 1 - Effective medium 2
Aex.SetRegion(1, Aex_val) // Exchange in J/m^3
Msat.SetRegion(1, Msat_val) //Saturation magnetisation in A/m
Ku1.SetRegion(1, Ku1_val) // Anisotropy in J/m^3
AnisU.SetRegion(1, vector(0, 0, 1)) //Uniaxial anisotropy direction 
Dbulk.SetRegion(1, Dbulk_val)
Dind.SetRegion(1, Dind_val) //Interfacial DMI in J/m^2
alpha.SetRegion(1, alpha_val) // zDamping

num_layers := 2

// Simulation mesh
SetGridsize(256, 256, num_layers)
SetCellsize(4e-9, 4e-9, 4e-9)

// Set regions to layers
for layer_number:=0; layer_number<num_layers; layer_number+= 1 {
	// layer0: region1, layer1: region0,...
	defRegion(Mod(layer_number+1, 2), layer(layer_number))
}

// set interlayer exchange coupling
ext_scaleExchange(0, 1, 0.1)	

// set external field and temp
B_ext = vector(0, 0, 0)
Temp = 0.0

// choose solver
SetSolver(5) 

// ----------------------------

m = Uniform(0,0,1)

sk_transl_pos := -400e-9
sk_delta := 150e-9

// put in some skyrmions
for i:=0; i<5; i++{
	m.setInShape(cylinder(50e-9, inf).transl(sk_transl_pos+sk_delta*i,0,0), NeelSkyrmion(1,-1).scale(1,1,1).transl(sk_transl_pos+sk_delta*i,0,0))
}


// run until MaxTorque is small enough
RunWhile(MaxTorque>1e-3)
for layer_number:=0; layer_number<num_layers; layer_number+= 1 {
	// layer0: region1, layer1: region0,...
	Snapshot(CropLayer(m, layer_number))
}